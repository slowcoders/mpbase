buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group 'org.slowcoders'
version '1.0.0'

bintray {
    user = findProperty('maven.username')
    key = findProperty('maven.password')
    publications = ['publication']

    publish = true

    pkg {
        repo = 'slowcoders'
        name = 'serialize'
        vcsUrl = 'https://github.com/slowcoders/serialize.git'
        version {
            name = project.version
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

publishing {
    publications {
        publication(MavenPublication) {
            pom.withXml {
                asNode().dependencies.dependency.each { dep ->
                    def _package = dep.groupId.last().value().last() + ':' + dep.artifactId.last().value().last()
                    if(_package in excludeInPublish) {
                        assert dep.parent().remove(dep)
                    }
                }
            }
            from components.java
            groupId project.group
            artifactId = 'serialize'
            version project.version

            artifact sourcesJar
        }
    }
}

repositories {
    jcenter()
}


sourceSets {
    main {
        java {
            srcDirs += 'src/main/java'
        }
    }
}

ext {
    targetPlatform = 'java'
    excludeInPublish = [
            'com.google.guava:guava'
    ]
}

dependencies {
    if (project.targetPlatform == 'android') {
        implementation group: 'com.google.guava', name: 'guava', version: '25.1-android'
    } else {
        implementation group: 'com.google.guava', name: 'guava', version: '25.1-jre'
    }
    implementation group: 'joda-time', name: 'joda-time', version: '2.9.9'
}
